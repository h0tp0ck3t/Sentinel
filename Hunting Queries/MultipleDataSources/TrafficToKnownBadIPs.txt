// Name: Traffic to known bad IPs
//
// Id: 49686bba-4a30-4987-97cd-d53a644d0ac6
//
// Description:
// Microsoft tracks a significant number of threat actors/malware/botnets etc so as to protect its products and services.
// The query shows traffic to known malicious IPs associated with various spam campaigns, botnets , virus etc. 
// Examining traffic to these known malicious IPs is a potential avenue to discover attacks in your environment.
// Data feeds used -  W3CIISLog , DnsEvents , WindowsFirewall, WireData , CommonSecurityLog
//  Union has been done  using Fuzzy logic so that even if at least one data feed from above is available the query would execute
//
// DataSource: #CommonSecurityLog,#DnsEvents,#W3CIISLog,#WireData,#WindowsFirewall
//
// Tactics: #C2, #Exfiltration
//
union 
(union isfuzzy=true
(W3CIISLog | extend Country=RemoteIPCountry, Latitude=RemoteIPLatitude, Longitude=RemoteIPLongitude),
(DnsEvents | extend Country= RemoteIPCountry, Latitude = RemoteIPLatitude, Longitude = RemoteIPLongitude | extend Computer = ClientIP),
//(WireData | where Direction != 'Outbound' | extend Country=RemoteIPCountry, Latitude=RemoteIPLatitude, Longitude=RemoteIPLongitude),
(WindowsFirewall | where CommunicationDirection != 'SEND' | extend Country=MaliciousIPCountry, Latitude=MaliciousIPLatitude, Longitude=MaliciousIPLongitude),
(CommonSecurityLog | where CommunicationDirection != 'Outbound' | extend Country=MaliciousIPCountry, Latitude=MaliciousIPLatitude, Longitude=MaliciousIPLongitude, Confidence=ThreatDescription, Description=ThreatDescription)
| where isnotempty(MaliciousIP) and isnotempty(Country) and isnotempty(Latitude) and isnotempty(Longitude) | extend TrafficDirection="InboundOrUnknown"),
(union isfuzzy=true
//(WireData | where Direction == 'Outbound' | extend Country=RemoteIPCountry, Latitude=RemoteIPLatitude, Longitude=RemoteIPLongitude),
(WindowsFirewall | where CommunicationDirection == 'SEND' | extend Country=MaliciousIPCountry, Latitude=MaliciousIPLatitude, Longitude=MaliciousIPLongitude),
(CommonSecurityLog | where CommunicationDirection == 'Outbound' | extend Country=MaliciousIPCountry, Latitude=MaliciousIPLatitude, Longitude=MaliciousIPLongitude, Confidence=ThreatDescription, Description=ThreatDescription)
| where isnotempty(MaliciousIP) and isnotempty(Country) and isnotempty(Latitude) and isnotempty(Longitude) | extend TrafficDirection="Outbound")
| project TimeGenerated, IndicatorThreatType, Confidence, Severity, Computer, ProcessName, SentBytes, ReceivedBytes, TenantId, ReportReferenceLink, Type
//, LocalIP, LocalPortNumber, RemoteIP, RemotePortNumber, Direction
// Uncomment this to get more detailed breakdown -- but may massively increase number of rows in result set
//, MaliciousIP , Malcious_Domain = Name
| parse kind = regex ReportReferenceLink with "tags=((webbot\\+)|(controller\\+)|(bot\\+))?" Family:string '&languageCode' *
| summarize makeset(Computer) by IndicatorThreatType, Severity, Family, Type, ReportReferenceLink 
// Uncomment this to get more detailed breakdown -- but may massively increase number of rows in result set
//, MaliciousIP , Malicious_Domain 
