// Name: Anomalous Azure Active Directory apps based on authentication location
//
// Id: 73ac88c0-f073-4b23-8ac4-9f40ea11308d
//
// Description: This query over Azure AD sign-in activity highlights Azure AD apps with 
// an unusually high ratio of distinct geolocations versus total number of authentications
//
// DataSource: #SigninLogs
//
// Tactics: #InitialAccess
//
let timeRange=ago(14d);
let azureSignIns = 
SigninLogs
| where TimeGenerated >= timeRange
| where SourceSystem == "Azure AD"
| where OperationName == "Sign-in activity"
| project TimeGenerated, OperationName, AppDisplayName , Identity, UserId, UserPrincipalName, Location, LocationDetails , ClientAppUsed , DeviceDetail, ConditionalAccessPolicies;
azureSignIns
| extend locationString= strcat(tostring(LocationDetails["countryOrRegion"]), "/", tostring(LocationDetails["state"]), "/", tostring(LocationDetails["city"]), ";" , tostring(LocationDetails["geoCoordinates"]))
| summarize rawSigninCount=count(), countByAccount=dcount(UserId), locationCount=dcount(locationString) by AppDisplayName
| where rawSigninCount < 1000                     // tail - pick a threshold to rule out the very-high volume Azure AD apps
| where locationCount>countByAccount             // more locations than accounts
| where 1.0*rawSigninCount / locationCount > 0.8 // almost as many / more locations than sign-ins!
| order by rawSigninCount  desc
| join kind = leftouter (
   azureSignIns 
) on AppDisplayName 
| project AppDisplayName, TimeGenerated , Identity, rawSigninCount, countByAccount , locationCount,  locationString= strcat(tostring(LocationDetails["countryOrRegion"]), "/", tostring(LocationDetails["state"]), "/", tostring(LocationDetails["city"]), ";" , tostring(LocationDetails["geoCoordinates"]))
| order by AppDisplayName, TimeGenerated desc
