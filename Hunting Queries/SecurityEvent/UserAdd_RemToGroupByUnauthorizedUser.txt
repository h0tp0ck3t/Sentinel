// Name: User account added or removed from a security group by an unauthorized user
// Description: User account added or removed from a security group by an unauthorized user, pass in a list
//
// Id: d57f675c-ad6c-44d0-95fb-3bf707e70155
//
// DataSource: #SecurityEvent
//
// Tactics: #Persistence, #Discovery, #LateralMovement, #PrivilegeEscalation
//
// Create DataTable with your own values, example below shows dummy usernames that are authorized and for what domain
let List = datatable(AuthorizedUser:string, Domain:string)["Bob", "Domain", "joe", "domain", "MATT", "DOMAIN"];
let timeframe = 1d;
SecurityEvent
| where TimeGenerated >= ago(timeframe)
| where EventID in (4728, 4729, 4732, 4733, 4746, 4747, 4751, 4752, 4756, 4757, 4761, 4762)
| join kind= leftanti (
    List
    | project SubjectUserName = tolower(AuthorizedUser), SubjectDomainName = toupper(Domain)
) on SubjectUserName, SubjectDomainName
| project TimeGenerated, Computer, Account, SubjectUserName, SubjectDomainName, TargetAccount, EventID, Activity