// Name: sharepoint downloads from devices associated with previously unseen user agents.
//
// Id: f2367171-1514-4c67-88ef-27434b6a1093
//
// Description: tracking via user agent is one way to differentiate between types of connecting device. 
// In homogeneous enterprise environments the user agent associated with an attacker device may stand out as unusual. 
//
// DataSource: #OfficeActivity
//
// Tactics: #Exfiltration
//
let historicalActivity=
OfficeActivity
| where  RecordType == "SharePointFileOperation"
| where Operation in ("FileDownloaded", "FileUploaded")
| where TimeGenerated between(ago(30d)..ago(7d))
| summarize historicalCount=count() by UserAgent;
let recentActivity = OfficeActivity
| where  RecordType == "SharePointFileOperation"
| where Operation in ("FileDownloaded", "FileUploaded")
| where TimeGenerated > ago(1d) 
| summarize recentCount=count() by UserAgent;
recentActivity | join kind = leftanti (
   historicalActivity 
) on UserAgent
| project RecordType = "SharePointFileOperation", UserAgent, recentCount
| order by recentCount asc, UserAgent; 
