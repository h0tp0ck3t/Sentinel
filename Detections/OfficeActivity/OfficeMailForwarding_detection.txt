// Name: multiple users forwarding Office mail to same destination
//
// Id: 871ba14c-88ef-48aa-ad38-810f26760ca3
//
// Description: This query over Office Activity audit data highlights cases where multiple user mailboxes are
// set up to be forwarded to the same destination. This could be an attacker-controlled destination mailbox
// configured to collect mail from multiple compromised user accounts.
//
// DataSource: #OfficeActivity
//
// Severity: Medium
//
// QueryFrequency: 24
//
// QueryPeriod: 30d
//
// AlertTriggerOperator: gt
//
// AlertTriggerThreshold: 0
//
// Techniques: #Exfiltration
//
OfficeActivity
| where Operation == "Set-Mailbox"
| where TimeGenerated >= ago(30d)
| where Parameters contains "ForwardingSmtpAddress"
| extend parsed=parse_json(Parameters)
| extend parameterName=parsed[1].Name, fwdingDestination=tostring(parsed[1].Value)
| where fwdingDestination != ""
| summarize TimeGenerated=max(TimeGenerated) by UserId, fwdingDestination
| summarize TimeGenerated=max(TimeGenerated), userCount=dcount(UserId), makelist(UserId) by fwdingDestination
| where userCount >1 and TimeGenerated >= ago(24h)
  