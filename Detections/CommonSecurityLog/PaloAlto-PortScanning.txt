// Name: Possible Internal to External PortScanning based on Palo Alto designation of "incomplete application protocol"
//
// Description: This query gathers network information such as Source and Destination Ports and IPAddresses as well as ApplicationProtocol from Palo Alto Network devices. 
// The Alert returns a list of internal Source IPs (10.x.x.x Hosts) that have triggered 10 or more non-graceful tcp server resets from one or more Destination IPs which 
// results in an "ApplicationProtocol = incomplete" designation.
// The server resets coupled with an "Incomplete" ApplicationProtocol designation can be an indication of internal to external port scanning or probing attack. 
// https://knowledgebase.paloaltonetworks.com/KCSArticleDetail?id=kA10g000000ClUvCAK
// https://knowledgebase.paloaltonetworks.com/KCSArticleDetail?id=kA10g000000ClTaCAK
//
// Id: 5b72f527-e3f6-4a00-9908-8e4fee14da9f
//
// Severity: Low
//
// QueryFrequency: 1h
//
// QueryPeriod: 1h
//
// AlertTriggerOperator: gt
//
// AlertTriggerThreshold: 0
//
// DataSource: #CommonSecurityLog
//
// Tactics: #Discovery
// 
let timeframe = 1h;
CommonSecurityLog 
| where TimeGenerated > ago(timeframe) 
| where isnotempty(DestinationPort) and DeviceAction !in ("reset-both", "deny") 
// filter out common usage ports. Add ports that are legitimate for your environment
| where DestinationPort !in ("443", "53", "389", "80", "0", "880", "8888", "8080")
| where ApplicationProtocol == "incomplete" 
// filter out IANA ephemeral or negotiated ports as per https://en.wikipedia.org/wiki/Ephemeral_port
| where DestinationPort !between (toint(49512) .. toint(65535)) 
| where Computer != "" 
| where DestinationIP !startswith "10." 
// *** Note: | where DestinationIP startswith "10."  // for reverse (internal is resetting the connection)
// Filter out any graceful reset reasons AGED OUT Occurs when a TCP session closes with a FIN due to aging out. TCP FIN Occurs when a TCP FIN is used to gracefully close half or both sides of a connection.
| where AdditionalExtensions !has "reason=aged-out" 
| where AdditionalExtensions !has "reason=tcp-fin" 
// Unremark one of the following to trigger on specific TCP reset reasons-  See Palo Alto article - https://knowledgebase.paloaltonetworks.com/KCSArticleDetail?id=kA10g000000ClUvCAK
// | where AdditionalExtensions has "reason=tcp-rst-from-server"  // TCP RST-server - Occurs when the server sends a TCP reset to the client
// | where AdditionalExtensions has "reason=tcp-rst-from-client"  // TCP RST-client - Occurs when the client sends a TCP reset to the server
| extend reason = tostring(split(AdditionalExtensions, ";")[3])
| summarize count() by SourceIP, ApplicationProtocol, reason, DestinationPort, Protocol, DeviceVendor, DeviceProduct, DeviceAction, DestinationIP
| where count_ >= 10
| summarize makeset(DestinationIP), totalcount = sum(count_)  by SourceIP, ApplicationProtocol, reason, DestinationPort, Protocol, DeviceVendor, DeviceProduct, DeviceAction
