// Name: AD User Created/Password not set with 24-48 hours
//
// Id: 62085097-d113-459f-9ea7-30216f2ee6af
//
// Description: This alert is triggered whenever a new account is created with a default password and password is not changed within 24-48 hours.
// Simple version, can be more precise with Windowing, but not necessary if run as an alert on a daily basis.
// Effectively, there is an event 4722 indicating a user enabled and no event 4723 indicating a password was changed within in that day or the next day
//
// Severity: Low
//
// QueryFrequency: 24h
//
// QueryPeriod: 2d
//
// AlertTriggerOperator: gt
//
// AlertTriggerThreshold: 0
//
// DataSource: #SecurityEvent
//
// Tactics: #Persistence
//
SecurityEvent
// include yesterday and not today, so we can confirm a match occurs within the next day
| where TimeGenerated >= startofday(ago(2d)) and TimeGenerated <= startofday(ago(1d))  
// 4722: User Account Enabled
| where EventID == 4722 
// Removing Machine Accounts
| where TargetUserName !endswith "$" 
| join kind= leftanti (
	SecurityEvent
	//verify over the last 2 days
	| where TimeGenerated >= startofday(ago(2d))
	// Attempt made to change password
	| where EventID == 4723
	// Removing Machine Accounts
	| where TargetUserName !endswith "$"
) on TargetUserName, TargetDomainName
| project TimeGenerated, EventID, TargetUserName, TargetDomainName, SubjectUserName