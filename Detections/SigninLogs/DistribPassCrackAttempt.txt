// Name: Distributed Password cracking attempts.
// Id: bfb1c90f-8006-4325-98be-c7fffbc254d6
//
// Description: This query over Azure AD sign-in activity highlights distributed password cracking attempt.
// The premise is simple wherein the query looks for unusually high number of failed password attempts coming
// from multiple location for a user account.
//     50053   Account is locked because the user tried to sign in too many times with an incorrect user ID or password.
//     50055   Invalid password, entered expired password.
//     50056   Invalid or null password - Password does not exist in store for this user.
//     50126    Invalid username or password, or invalid on-premises username or password.
//
// DataSource: #SigninLogs
//
// Severity: Medium
//
// QueryFrequency: 24
//
// QueryPeriod: 24
//
// AlertTriggerOperator: gt
//
// AlertTriggerThreshold: 0
//
// Tactics: #InitialAccess
//
let timeRange = ago(24h);
SigninLogs
| where TimeGenerated >= timeRange
| where OperationName == "Sign-in activity"
// Error codes that we want to look at as they are related to the use of incorrect password.
| where ResultType in ("50126", "50053" , "50055", "50056")
| extend OS = DeviceDetail.operatingSystem, Browser = DeviceDetail.browser 
| extend LocationString= strcat(tostring(LocationDetails["countryOrRegion"]), "/", tostring(LocationDetails["state"]), "/", tostring(LocationDetails["city"]))
| summarize StartTimeUtc = min(TimeGenerated), EndTimeUtc = max(TimeGenerated), SigninCount=count(), LocationCount=dcount(LocationString) by UserPrincipalName, IPAddress, LocationString, AppDisplayName, ResultDescription, tostring(Browser), tostring(OS)
| project StartTimeUtc, EndTimeUtc, SigninCount, LocationCount, UserPrincipalName, IPAddress, LocationString, AppDisplayName, ResultDescription, Browser, OS
// Setting a generic threshold - Can be different for different environment
| where SigninCount > 30                                     
// Setting a generic threshold for location  - Can be different for different environment
| where LocationCount >=  3