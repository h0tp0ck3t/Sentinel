// Name: DNS Name Lookup Query from most prevalent ClientIP to remote IP Address
// Description: Summary of Top 10 Client IP and Domain Names for a given remote IPAddress from DnsEvent Lookup Query data.
//
// Entity: IPAddress
// Input: IPAddress
// Output: ClientIP
//
// QueryPeriod:  +-3h
//
// Data Source: #DnsEvents
//
// Tactics: #CommandAndControl, #Exfiltration
//
let GetAllIPByClientIP = (suspiciousEventTime:datetime, v_IPAddress:string){
let v_StartTime = suspiciousEventTime-3h;
let v_EndTime = suspiciousEventTime+3h;
DnsEvents
| where TimeGenerated between (v_StartTime .. v_EndTime)
| where SubType == "LookupQuery"
| where IPAddresses contains v_IPAddress
| summarize min(TimeGenerated), max(TimeGenerated), makeset(Name), count() by ClientIP, IPAddresses
| project StartTimeUtc = min_TimeGenerated, EndTimeUtc = max_TimeGenerated, ClientIP, DomainNames = set_Name, IPAddresses, count_ 
| top 10 by count_ desc nulls last
| project StartTimeUtc, EndTimeUtc, ClientIP, IPAddresses, DomainNames
};
// change datetime value and <ipaddress> value below
GetAllIPByClientIP(datetime('2019-02-05T10:36:07Z'), "<ipaddress>")