// Name: Windows - Process Rare for Host
// Description: Lookup your interesting process and see on what hosts it is rare.  
// Less than 1% of the average for 30 days and less than a count of 100 on a given host or less than a 14 count on a given host from the last 7 days
//
// Entity: Process
// Input: ProcessName
// Output: Hosts
//
// QueryPeriod: -7d, change as needed
//
// Data Source: Syslog
//
// Tactics: #Execution, #Persistence, #Discovery, #Lateral Movement, #Collection
//
let GetHostWithRareProcess = (suspiciousEventTime:datetime, v_Process:string){
let v_StartTime = suspiciousEventTime-7d;
let v_EndTime = suspiciousEventTime-1m;
SecurityEvent
| where EventID == 4688
| where TimeGenerated between (v_StartTime .. v_EndTime)
| where NewProcessName has v_Process
| summarize count() by Computer, NewProcessName
| join kind= inner (
    SecurityEvent
    // get avg from last 30 days
    | where TimeGenerated >= ago(30d)
    | where EventID == 4688
	| where NewProcessName has v_Process
    | summarize count() by Computer, NewProcessName
    | summarize avg(count_) by NewProcessName
    ) on NewProcessName
// attempt to control for noise, but not miss low prevalence count with low average count
| where count_ < 14 or (count_ <= avg_count_*0.01 and count_ < 100)
| join kind= inner (
    SecurityEvent
    | where EventID == 4688
    | where TimeGenerated between (v_StartTime .. v_EndTime)
	| where NewProcessName has v_Process
    | summarize min(TimeGenerated), max(TimeGenerated) by Computer, NewProcessName
) on Computer, NewProcessName
| project min_TimeGenerated, max_TimeGenerated, EventCount = count_, AvgEventCountAllSystems = avg_count_, Computer, NewProcessName
| top 10 by EventCount asc nulls last 
| project min_TimeGenerated, max_TimeGenerated, Computer, NewProcessName, EventCount, AvgEventCountAllSystems 
};
// change datetime value and <processname> value below
GetHostWithRareProcess(datetime('2019-01-31T10:36:07Z'), "<processname>")