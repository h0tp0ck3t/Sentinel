// Name: Windows Hosts With This Process
// Description: Any host with this same process in the range of +-1h
//
// Entity: Process
// Input: ProcessName
// Output: Hosts
//
// QueryPeriod: +-1h, change as needed
//
// Data Source: SecurityEvent
//
// Tactics: #Execution, #Persistence, #Discovery, #Lateral Movement, #Collection
//
let GetHostsWithProcess = (suspiciousEventTime:datetime, v_Process:string){
let v_StartTime = suspiciousEventTime-1h;
let v_EndTime = suspiciousEventTime+1h;
SecurityEvent
| where TimeGenerated between (v_StartTime .. v_EndTime)
| where EventID == 4688
| where NewProcessName has v_Process
| summarize min(TimeGenerated), max(TimeGenerated) by Computer, Account, NewProcessName, CommandLine
| project min_TimeGenerated, max_TimeGenerated, Computer, Account, NewProcessName, CommandLine
| top 10 by min_TimeGenerated asc nulls last 
};
// change datetime value and <processname> value below
GetHostsWithProcess(datetime('2019-01-30T10:36:07Z'), "<processname>")